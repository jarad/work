kickermodel {
  for (i in 1:N) {
    y[i] ~ dbin(pi[i],1)                          # each kick gets its own row
    # kicker specific random intercept and slope
    # logistic link
    logit(pi[i]) <- b[kicker[i],1]+b[kicker[i],2]*dist[i]
  }

  for (k in 1:K) {
    for (j in 1:p) {
      # kicker effects, xi for scaled invWishart
      b[k,j] <- b.raw[k,j]*xi[j]                   
    }
    b.raw[k,1:p] ~ dmnorm(mu.raw[1:p],Tau.raw[1:p,1:p])    
  }

  # Prior for mean
  for (j in 1:p) { 
    mu[j] <- mu.raw[j]*xi[j]
    mu.raw[j] ~ dnorm(0.0,mu.prec) 
    xi[j] ~ dunif(0,100)           # No idea if the endpoint matters
  }   

  # Prior for covariance matrix
  Tau.raw[1:p,1:p] ~ dwish(R[1:p,1:p], df)
  df <- p+1
  Sigma.raw[1:p,1:p] <- inverse(Tau.raw[1:p,1:p])

  for (j in 1:p) {
    sigma[j] <- xi[j]*sqrt(Sigma.raw[j,j])
    for (k in 1:j) {
      corr[j,k] <- xi[j]*xi[k]*Sigma.raw[j,k]/(sigma[j]*sigma[k])
    }
  }
}


